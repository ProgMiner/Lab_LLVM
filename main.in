
function sim_set_pixel(x: int, y: int, argb: int);
function sim_flush();

function sim_rand(): int;

function get_gen_value(gen: [int], x: int, y: int): int {
    x = (x % 256 + 256) % 256;
    y = (y % 128 + 128) % 128;

    var idx = y * 256 + x;
    (gen[idx / 32] >> (idx % 32)) & 1;
}

function set_gen_value(gen: [int], x: int, y: int, v: int) {
    x = (x % 256 + 256) % 256;
    y = (y % 128 + 128) % 128;

    var idx = y * 256 + x;

    if (v) {
        gen[idx / 32] |= 1 << idx % 32;
    } else {
        gen[idx / 32] &= ~(1 << idx % 32);
    }
}

function draw_gen(gen: [int]) {
    for (var y = 0; y < 128; ++y) {
        for (var x = 0; x < 256; ++x) {
            // 0xFF000000 + 0xFF00 * get_gen_value(gen, x, y)
            sim_set_pixel(x, y, 4278190080 + 65280 * get_gen_value(gen, x, y));
        }
    }

    sim_flush();
}

function calc_gen(next_gen: [int], prev_gen: [int]): int {
    var has_changes = false;
    var has_living = false;

    for (var y = 0; y < 128; ++y) {
        for (var x = 0; x < 256; ++x) {
            var neighbours = 0;

            for (var dy = -1; dy <= 1; ++dy) {
                for (var dx = -1; dx <= 1; ++dx) {
                    if (dx == 0 && dy == 0) {
                        continue;
                    }

                    neighbours += get_gen_value(prev_gen, x + dx, y + dy);
                }
            }

            var old_value = get_gen_value(prev_gen, x, y);
            var next_value =
                if (old_value) neighbours == 2 || neighbours == 3
                else neighbours == 3;

            if (old_value != next_value) {
                has_changes = true;
            }

            if (next_value) {
                has_living = true;
            }

            set_gen_value(next_gen, x, y, next_value);
        }
    }

    has_changes || has_living;
}

function main() {
    var gen1: int[1024];
    var gen2: int[1024];

    var next_gen: [int] = gen1;
    var prev_gen: [int] = gen2;

    for (var y = 0; y < 128; ++y) {
        for (var x = 0; x < 256; ++x) {
            set_gen_value(prev_gen, x, y, sim_rand() % 2);
        }
    }

    draw_gen(prev_gen);

    while (calc_gen(next_gen, prev_gen)) {
        draw_gen(next_gen);

        var tmp = prev_gen;
        prev_gen = next_gen;
        next_gen = tmp;
    }
}
